// find all players with multiple positions including DEF
MATCH (p:Player)-[:PLAYS_AS]->(pos:Position)
WITH p, collect(DISTINCT pos.name) AS positions
WHERE size(positions) > 1 AND "DEF" IN positions

// get total fixtures played
WITH p, positions, COUNT{(p)-[:PLAYED_IN]->()} AS total_fixtures

// process each fixture for DEF scoring comparison
MATCH (p)-[r:PLAYED_IN]->(f:Fixture)

WITH p, positions, total_fixtures, r, f,
     coalesce(r.minutes, 0) AS mins,
     coalesce(r.goals_scored, 0) AS goals,
     coalesce(r.assists, 0) AS assists,
     coalesce(r.clean_sheets, 0) AS cs,
     coalesce(r.goals_conceded, 0) AS gc,
     coalesce(r.bonus, 0) AS bonus,
     coalesce(r.yellow_cards, 0) AS yc,
     coalesce(r.red_cards, 0) AS rc,
     coalesce(r.penalties_missed, 0) AS penmiss,
     coalesce(r.own_goals, 0) AS og,
     r.total_points AS actual_points

// recompute points as if the player WAS a true Defender (DEF rules)
WITH p, positions, total_fixtures, r, f, actual_points,
     // Appearance points
     (CASE WHEN mins >= 60 THEN 2 WHEN mins > 0 THEN 1 ELSE 0 END) +
     // Goal scored as DEF = 6 points
     goals * 6 +
     // Clean sheet as DEF = 4 points (only if â‰¥60 mins)
     cs * 4 * (CASE WHEN mins >= 60 THEN 1 ELSE 0 END) +
     // Assists
     assists * 3 +
     // Bonus
     bonus +
     // Goals conceded penalty (-1 per 2 goals)
     - floor(gc / 2) -
     // Cards and errors
     yc - (rc * 3) - (penmiss * 2) - (og * 2)
     AS def_score

// process all fixtures, collecting matching and non-matching
WITH p, positions, total_fixtures,
     collect(CASE WHEN actual_points <> def_score THEN {
         def_score: toFloat(def_score),
         season: f.season,
         fixture_number: f.fixture_number,
         matches_def: (actual_points = def_score),
         total_points: actual_points
     } ELSE null END) AS all_fixture_details


RETURN p.player_name AS player,
       size(all_fixture_details) AS not_def_fixtures,
       total_fixtures,
       all_fixture_details AS fixture_details